---
author: Masaki Waga
title: Frequent Message Detection
---

This is an example to detect too frequent messages sent from the same
address using [SyMon](https://github.com/MasWag/SyMon/). This example
consists of a Python script `convert_log.py` to clean up and construct a
log file for SyMon and the files required by SyMon.

# Requirements

- [SyMon](https://github.com/MasWag/SyMon/)
- Python3
- Emacs
  - to tangle the resulting files from frequent.org

# Usage

The usage of the generated files is as follows:

``` bash
sudo cat /var/log/exim4/mainlog |
    ./convert_log.py |
    ./frequent.symon |
    ./pretty_print.sh
```

In the converted log, the timestamp is the duration since the beginning
of the day in seconds.

You can re-generate `frequent.symon` by `make`. You can also generate
the document with `make doc`

# Events

We monitor the following event:

- `arrival`: The event showing the message arrival. This is shown as
  `<=` in the raw log.

Each `arrival` event is tied with a string `sender` corresponding to the
sender's address. The corresponding signature for SyMon is as follows:

``` symon
signature arrival {
    sender: string;
}
```

# Specification

What we want to do in the monitoring process is as follows:

- It waits for `arrival` from the (parameterized) address.
- After the `arrival`, it counts the `arrival` from the same address for
  3 seconds.
- We report if the count is more than 10.

To represent this specification, we need a parametric string value
representing the current sender's address and a counter of the number to
arrivals.

``` symon
var {
    current_sender: string;
    count: number;
}
```

First, we ignore irrelevant arrivals.

``` symon
one_or_more {
    arrival(sender)
}
```

Then, we have a relevant arrival.

``` symon
# Nondeterministically start counting for current_sender
arrival( sender | sender == current_sender | count := 1 )
```

After that, we count the number of the arrival from the same sender for
3 seconds up to 10, while ignoring arrival from other senders.

``` symon
one_or_more {
    one_of {
        arrival( sender | sender != current_sender && count <= 10 )
    } or {
        arrival( sender | sender == current_sender && count < 10 | count := count + 1 )
    }
}
```

We stop counting and report if we have another arrival when count is 10,
i.e., we have 11th arrival.

``` symon
arrival( sender | sender == current_sender && count = 10 | count := count + 1 )
```

And, these thing must happen within 3 seconds.

``` symon
within (<= 3) {
    one_or_more {
        one_of {
            arrival( sender | sender != current_sender && count <= 10 )
        } or {
            arrival( sender | sender == current_sender && count < 10 | count := count + 1 )
        }
    };
    arrival( sender | sender == current_sender && count = 10 | count := count + 1 )
}
```

Overall, the following shows the specification.

``` symon
var {
    current_sender: string;
    count: number;
}

signature arrival {
    sender: string;
}

one_or_more {
    arrival(sender)
};
# Nondeterministically start counting for current_sender
arrival( sender | sender == current_sender | count := 1 );
within (<= 3) {
    one_or_more {
        one_of {
            arrival( sender | sender != current_sender && count <= 10 )
        } or {
            arrival( sender | sender == current_sender && count < 10 | count := count + 1 )
        }
    };
    arrival( sender | sender == current_sender && count = 10 | count := count + 1 )
}
```

# Example Execution

Here is an example of the log generated by `convert_log.py`.

``` txt
arrival   alice@example.com   0.0
arrival   bob@example.org 0.3
arrival   alice@example.com   0.5
arrival carol@example.net 0.7
arrival   alice@example.com   0.8
arrival   alice@example.com   1.0
arrival   bob@example.org 1.2
arrival   alice@example.com   1.5
arrival   alice@example.com   1.7
arrival   alice@example.com   1.9
arrival   carol@example.net   2.0
arrival   alice@example.com   2.2
arrival   alice@example.com   2.4
arrival   bob@example.org 2.5
arrival   alice@example.com   2.7
arrival   carol@example.net   2.9
arrival   alice@example.com   3.1
arrival   bob@example.org 3.25
arrival   alice@example.com   3.3
arrival   bob@example.org 3.4
```

Here is the command to run SyMon with a monitoring result:

``` bash
cat example.log | 
    ./frequent.symon |
    ./pretty_print.sh
```

``` example
@3.300000.    (time-point 18) current_sender: alice@example.com   count: 11
```
